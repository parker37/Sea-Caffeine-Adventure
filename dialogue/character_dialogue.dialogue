#region Clam
~ clam
if start_quest:
	Pearl: Hi, I'm Pearl. I love making new friends, stop by anytime!
	set dialogue_on = false
	=> END
	
if pearl_first_talk:
	Pearl: Hi! Oh my gosh I love visitors. How can I help you today?
	Pearl: Oh you're trying to open a coffee shop? I would be happy to help. But first, I have to know if this coffee is any good.
	set pearl_first_talk = false
	
if !completed_clam:
	Pearl: Do you have any coffee for me to try?
	- Right here
		if inventory.check_for_item("darkcoffee"):
			do inventory.remove_item("darkcoffee")
			Pearl: Oh my! This is fantastic. Everyone has to know about this.
			Pearl: I'll gladly help. Go let your friend know that we have a business!
			set completed_clam = true
		else:
			Pearl: Oh, no coffee yet? Don't worry, I'm patient.
	- I'm working on it

else if !has_table:
	Pearl: Just let me know what I can do to help!
	
else if has_table:
	Pearl: How can I help you today?
	- Cancel
	- Make coffee
		if turned_in_salt:
			Pearl: Of course. What recipe do you want to make?
			- Dark Coffee
				do inventory.add_item(darkcoffee_item)
			- Green Coffee
				do inventory.add_item(greencoffee_item)
			- Milk Coffee
				do inventory.add_item(milkcoffee_item)
			- Sea Salt Foam Coffee
				do inventory.add_item(seasaltfoam_item)
		else if turned_in_milk:
			Pearl: Of course. What recipe do you want to make?
			- Dark Coffee
				do inventory.add_item(darkcoffee_item)
			- Green Coffee
				do inventory.add_item(greencoffee_item)
			- Milk Coffee
				do inventory.add_item(milkcoffee_item)
		else if turned_in_seaweed:
			Pearl: Of course. What recipe do you want to make?
			- Dark Coffee
				do inventory.add_item(darkcoffee_item)
			- Green Coffee
				do inventory.add_item(greencoffee_item)
		else:
			Pearl: Of course. What recipe do you want to make?
			- Dark Coffee
				do inventory.add_item(darkcoffee_item)
	- Trash coffee
		set is_trashing = true
		Pearl: Which coffee would you like to trash?
		- Cancel
		- Slot 1
			do inventory.remove_item(0)
		- Slot 2
			do inventory.remove_item(1)
		- Slot 3
			do inventory.remove_item(2)
		- Slot 4
			do inventory.remove_item(3)
		- Slot 5
			do inventory.remove_item(4)
		- Slot 6
			do inventory.remove_item(5)
		
set dialogue_on = false
=> END
#endregion

#region Charles
~ charles
if start_quest:
	Charles: Hey! Over here!
	Charles: I need your help with something. I love coffee and want to sell it down here.
	Charles: Problem is, I don't have anything that I can run the shop with. I will need ingredients and a place to set up shop.
	Charles: Think you're up for the job?
	- I guess...
	- I would love to help!
	set start_quest = false
	
if !turned_in_cup and !turned_in_squidink:
	Charles: First we need something to make coffee with, and also something to put it in...
	- I got some stuff for that
		if inventory.check_for_item("emptycup") and inventory.check_for_item("squidinkbottle"):
			do inventory.remove_item("emptycup")
			do inventory.remove_item("squidinkbottle")
			Charles: Oh wow! This will work great. I'll go work on a prototype.
			Charles: ...
			Charles: Here you go! My first squid ink coffee. You can do whatever you want with it.
			- Thanks
			set turned_in_cup = true
			set turned_in_squidink = true
			do inventory.add_item(darkcoffee_item)
			Charles: Now we need somewhere to make and sell the coffee.
		else:
			Charles: I need more than that. Come back to me when you have what I'm looking for.
	- I'm working on it
	
#check if has table
else if !has_table:
	Charles: I still need somewhere to sell the coffee. I don't have the sand dollars for that big fancy building yet.
	- I figured something out
		if completed_clam:
			Charles: Oh that's a fantastic idea! And Pearl was really willing to help? How nice.
			Charles: Now that we a table to make our coffee at, we can start creating more recipes.
			Charles: To make a cup of coffee, just talk to Pearl. We only have one recipe for now but we'll work on getting more.
			set has_table = true
		else:
			Charles: That doesn't really sound like an idea yet. Go explore some more and see if you can figure anything out.
	- I'm on it
	
else if has_table and !completed_sharks:
	if inventory.check_for_item("seaweed"):
		Charles: Oh, some seaweed? This is will make our coffee green and give it a unique flavor. I've given Pearl the new recipe so stop by her if you want to make it.
		do inventory.remove_item("seaweed")
		set turned_in_seaweed = true
		
	else if inventory.check_for_item("milkbottle"):
		Charles: Of course, how come I didn't think of this? Some milk with make our coffee less bitter and make it creamy. I've given Pearl the new recipe.
		do inventory.remove_item("milkbottle")
		set turned_in_milk = true
		
	else if inventory.check_for_item("rocksalt"):
		Charles: Salt? This is interesting. I could foam it up and add it on top. This will add a new flavor with a fun texture. Talk to Pearl to make the new recipe.
		do inventory.remove_item("rocksalt")
		set turned_in_salt = true
		
	else if !turned_in_salt:
		Charles: Let's work on getting new flavors. Ask around town and see what everyone is interested in.
	
	else:
		Charles: You've been a great help but I don't think I can be happy with just a table. And I can't ask Pearl to be here forever.
		Charles: My dream is to run a real coffee shop, out of a building.
		
else if completed_sharks:
	Charles: You did it. You got funding from the sharks, I finally have enough money to open a real shop.
	Charles: Thank you so much for your help! You're a good friend.
	
set dialogue_on = false
=> END
#endregion

#region Squid
~ squid
if start_quest:
	Riptide: What's up dude, you new around here? You don't know the famous Riptide?
	set dialogue_on = false
	=> END

if !completed_squid:
	Riptide: You want my ink? That stuff isn't free buddy.
	Riptide: Give me 5 sand dollars and its all yours.
	- Here you go
		if inventory.check_for_item("sanddollar", 5):
			do inventory.remove_item("sanddollar")
			do inventory.remove_item("sanddollar")
			do inventory.remove_item("sanddollar")
			do inventory.remove_item("sanddollar")
			do inventory.remove_item("sanddollar")
			Riptide: Thanks buddy. Here's the stuff.
			do inventory.add_item(squidinkbottle_item)
			set completed_squid = true
			set dialogue_on = false
			=> END
		else:
			Riptide: Stop my pulling my tentacle. I said 5. Come back later.
			set dialogue_on = false
			=> END
	- I'll be right back
else:
	Riptide: Haha, sucker. Squid ink is nowhere near worth 5 sand dollars.
set dialogue_on = false
=> END
#endregion

#region Pufferfish
~ pufferfish
if start_quest:
	Tough Guy: Beat it, kid.
	set dialogue_on = false
	=> END

if !completed_pufferfish:
	Tough Guy: Oh this stuff behind me? Just some delicious salt. But it's mine. If you want some of it, you're gonna have to go through me.
	Tough Guy: Its for some coffee? Why didn't you just say so? Bring me some delicious creamy coffee and I'll let you have a sample.
	- I have some right here
		if inventory.check_for_item("milkcoffee"):
			Tough Guy: Hm. I didn't think you would pull it off. Regardless, I'm a man of my word. Here you go.
			do inventory.remove_item("milkcoffee")
			do inventory.add_item(rocksalt_item)
			set completed_pufferfish = true
		else:
			Tough Guy: Don't test me, come back when you have the right stuff.
	- I'll get on it
			Tough Guy: You better.
else:
	Tough Guy: I've already helped you. Unless you're looking for a fight, I suggest you carry on.
set dialogue_on = false
=> END
#endregion

#region Whale
~ whale
if start_quest:
	Cresta: Hi darling, I'm Cresta. What a pleasure to meet you.
	set dialogue_on = false
	=> END
if !completed_whale:
	Cresta: You want.. [wait=1]my milk? That's strange. You say its to make coffee? Can you bring me one? And I want it to taste earthy. If you do this, I promise I can give you some milk.
	- Try this
		if inventory.check_for_item("greencoffee"):
			Cresta: This is perfect, exactly what I asked for. It's even my favorite color.
			Cresta: One milk, coming up.
			do inventory.remove_item("greencoffee")
			do inventory.add_item(milkbottle_item)
			set completed_whale = true
		else:
			Cresta: I'm sorry darling, but I want something more earthy. Something with plants in it, maybe.
	- I can get you some
		Cresta: I'll be here.
else:
	Cresta: I hope the milk worked well for your coffee. Maybe I'll stop by and get more seaweed coffee some day.
set dialogue_on = false
=> END
#endregion

#region Sharks
~ shark
if start_quest:
	Shark 1: We're the sharks and if you want something, we get it done.
	Shark 2: You tell us your idea, and if we like it, we give you the sand dollars to make it happen.
	Earl: Come back to talk to us when you have something to say.
	set dialogue_on = false
	=> END

if !completed_sharks:
	Shark 1: You want us to fund your coffee shop?
	Shark 2: Coffee, in the ocean? It doesn't sound very profitable.
	Earl: You'll have to bring us some proof that this is a viable business opportunity.
	Shark 1: Bring us each a coffee of our choice and then we can discuss.
	- What do you want?
		Shark 1: I want a coffee with a nice green color.
		Shark 2: I don't like bitter stuff.
		Earl: Give me something salty.
		set dialogue_on = false
		=> END
		
	- I have your coffee right here
		if inventory.check_for_item("greencoffee") and inventory.check_for_item("milkcoffee") and inventory.check_for_item("seasaltfoam"):
			Shark 1: Thanks!
			Shark 2: This is tasty.
			Earl: Fine. You have our financial support. But don't mess this up.
			set completed_sharks = true
			do inventory.remove_item("greencoffee")
			do inventory.remove_item("milkcoffee")
			do inventory.remove_item("seasaltfoam")
		else:
			Shark 2: This isn't what we're looking for.
			Shark 1: Come back later.
		set dialogue_on = false
		=> END
	- I'll be back later
else:
	Shark 1: This better bring in some profit.
	Shark 2: Well, at least you have one customer. Me.
	Earl: Are we done here?
set dialogue_on = false
=> END
#endregion